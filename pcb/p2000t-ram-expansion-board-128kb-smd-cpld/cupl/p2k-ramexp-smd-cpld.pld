Name     ATF1502ASV;
PartNo   00;
Date     03/29/2025;
Revision 1.0;
Designer Ivo Filot;
Company  None;
Assembly None;
Location None;
Device   f1502isptqfp44;

PROPERTY ATMEL {POWER_RESET = OFF};
PROPERTY ATMEL {PIN_KEEP = ON};
PROPERTY ATMEL {PREASSIGN = KEEP};

PIN  2 = A0;
PIN  3 = A1;
PIN  6 = A2;
PIN  8 = A3;
PIN 10 = A4;
PIN 11 = A5;
PIN 12 = A6;
PIN 13 = A7;
PIN 14 = MRQ_N;
PIN 15 = IORQ_N;
PIN 18 = RD_N;
PIN 19 = CS1_N;
PIN 20 = WR_N;
PIN 21 = RAMS2;
PIN 22 = RES_N;
PIN 23 = D0;
PIN 25 = D1;
PIN 27 = D2;
PIN 28 = D3;
PIN 30 = R4;
PIN 31 = R5;
PIN 33 = D6;
PIN 34 = D7;
PIN 35 = R0;
PIN 37 = A13;
PIN 38 = A14;
PIN 42 = R1;
PIN 43 = R2;
PIN 44 = R3;

/** Buried states **/
Pinnode = ADDRSEL;
Pinnode = [S3..0];
Pinnode = BANKCP;
Pinnode = BANKRD;
Pinnode = EO;

/** ------------------------------------------------------------------------**/
/** activate ROM chip on /MRQ and RAMS2                                     **/
/** ------------------------------------------------------------------------**/
CS1 = !(!MRQ_N & RAMS2);

/** ------------------------------------------------------------------------**/
/** Capture memory address 0x94 = 1001 0010                                 **/
/** ------------------------------------------------------------------------**/
ADDRSEL = (A7 & !A6 & !A5 & A4 & !A3 & A2 & !A1 & !A0);

/** ------------------------------------------------------------------------**/
/** Write BANK address to BANK register                                     **/
/** ------------------------------------------------------------------------**/
BANKCP = (!IORQ_N & !WR_N & ADDRSEL);
S0.d = D0;
S1.d = D1;
S2.d = D2;
S3.d = D3;
[S3..0].ck = BANKCP;
[S3..0].ar = !RES_N;

/** ------------------------------------------------------------------------**/
/** Output current BANK address from BANK register                          **/
/** ------------------------------------------------------------------------**/
BANKRD = (!IORQ_N & !RD_N & ADDRSEL);
D0 = BANKRD & S0;
D1 = BANKRD & S1;
D2 = BANKRD & S2;
D3 = BANKRD & S3;
D4 = !BANKRD;
D5 = !BANKRD;
D6 = !BANKRD;
D7 = !BANKRD;
D0.oe = BANKRD;
D1.oe = BANKRD;
D2.oe = BANKRD;
D3.oe = BANKRD;
D4.oe = BANKRD;
D5.oe = BANKRD;
D6.oe = BANKRD;
D7.oe = BANKRD;

/** ------------------------------------------------------------------------**/
/** Construct BANK address based on A13 and A14                             **/
/** When A13 and A14 are both high (E000 - FFFF), add 2 to the value in the **/
/** S0-S3 and output that to R0-R3. If not, just place A13 on R0            **/
/** The addition of "+2" is accomplished using a (S+2) ADDER                **/
/** ------------------------------------------------------------------------**/
EO = A13 & A14;
SUM0 = S0;                          /** BIT 0 always remains the same **/
SUM1 = !S1;                         /** BIT 1 always flips upon addition **/
C2   = S1;                          /** CARRY2 is always equal to BIT1 **/
SUM2 = (!S2 & C2) # (S2 & !C2);     /** SUM2 = S2 XOR C2 **/
C3   = S2 & C2;                     /** CARRY3 = S2 AND C2 **/
SUM3 = (!S3 & C3) # (S3 & !C3);     /** SUM3 = S3 XOR C3 **/
R0 = (EO & SUM0) # (!EO & A13);
R1 = EO & SUM1;
R2 = EO & SUM2;
R3 = EO & SUM3;